; ----------------------------------------------------------------------
; Handles a simple menu
; ----------------------------------------------------------------------

#include "../../teletextspectrum/teletext.inc"

#local
currentMenu:    defw    0               ; Address of menu being displayed

; Display a menu and wait for user to make a selection
;
; The menu definition is:
;       0 terminated title of menu
;       byte containing number of entries (n)
;       n bytes for the keys, in Upper case if letters
;       n 0 terminated strings for each option
;
; Entry:
;   HL      Address of menu definition
;
; Exit:
;   A       Key pressed
;   B       Index in menu of selected option
;   Carry   true if valid, false if break
;
showMenu::  CALL    header              ; Clear screen & show header
            LD      (currentMenu), HL   ; Save start of actual menu

            PUSH    HL
            LD      BC, 0x0401          ; Set text pos
            CALL    teletextSetPos
            POP     HL

            LD      A, 140              ; Ensure single height, fixes issue with
            CALL    oswrch              ; header

            LD      B, (HL)             ; Show Key labels
            PUSH    BC                  ; Save row count
l1:         INC     HL                  ; inc & save HL
            LD      A, (HL)             ; Write key then down to next line
            CALL    oswrch
            CALL    backdown1           ; Back & Down 1 char
            DJNZ    l1                  ; Loop back
            INC     HL                  ; Skip last entry so HL is first label

            POP     BC                  ; Get row count so we skip the vector
            PUSH    BC                  ; table for each option
l1a:        INC     HL
            INC     HL
            DJNZ    l1a

            POP     BC                  ; Restore row count
            LD      C, 0                ; Index of entry
l2:         PUSH    BC                  ; Save B
            PUSH    HL                  ; Save HL
            LD      A, 4                ; TAB(4,B+4)
            ADD     C
            LD      B, A
            LD      C, 4
            CALL    teletextSetPos

            CALL    greenText           ; Set label colour

            POP     HL                  ; Restore HL & write label
            CALL    writeString
            POP     BC                  ; Restore B
            INC     C                   ; next index
            DJNZ    l2                  ; Loop back

l3:         CALL    getKey              ; Key next key
            JP      NC, main            ; BREAK sends us to the main menu
            CALL    toUpper             ; to upper case

            LD      HL, (currentMenu)
            LD      B, (HL)
l4:         INC     HL
            LD      C, (HL)
            CP      C
            SCF                         ; Mark carry set incase match
            JR      Z, l5               ; Key match so look up address
            DJNZ    l4                  ; Loop for next potential match
            JP      l3                  ; Not found so loop for next key press

l5:         LD      HL, (currentMenu)   ; Find the vector entry
            LD      E, (HL)             ; Entry count
            LD      D, 0
            INC     HL                  ; Skip entry count
            ADD     HL, DE              ; Skip entry keys
            LD      A, E                ; Now we want (E-B)<<1 to get vector index
            SUB     B
            ADD     A, A                ; A<<1
            LD      E, A
            ADD     HL, DE              ; HL should point to the vector entry now
            LD      A, (HL)             ; Now get address at entry as no LD HL, (HL)
            INC     HL
            LD      H, (HL)
            LD      L, A
            JP      (HL)

    ; A-B = index of required entry

            SUB     B
            ADD     A                   ; A<<1

#endlocal
