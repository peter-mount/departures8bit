; ----------------------------------------------------------------------
; Handles a simple menu
; ----------------------------------------------------------------------

#include "../../teletextspectrum/teletext.inc"

; Display a menu and wait for user to make a selection
;
; The menu definition is:
;       0 terminated title of menu
;       byte containing number of entries (n)
;       n bytes for the keys, in Upper case if letters
;       n 0 terminated strings for each option
;
; Entry:
;   HL      Address of menu definition
;
; Exit:
;   A       Key pressed
;   B       Index in menu of selected option
;   Carry   true if valid, false if break
;
showMenu:
#local
            CALL    header              ; Clear screen & show header
            LD      (currentMenu), HL   ; Save start of actual menu

            PUSH    HL
            LD      BC, 0x0401          ; Set text pos
            CALL    teletextSetPos
            POP     HL

            LD      A, 140              ; Ensure single height, fixes issue with
            CALL    oswrch              ; header

            LD      B, (HL)             ; Get number of entries in B
            PUSH    BC                  ; Save entry count
l1:         INC     HL                  ; inc & save HL
            LD      A, (HL)             ; Write key then down to next line
            CALL    oswrch
            CALL    backdown1           ; Back & Down 1 char to move to next row
            DJNZ    l1                  ; Loop back
            INC     HL                  ; Skip last entry so HL is now first label

            POP     BC                  ; Get entry count so we skip the vector
            PUSH    BC                  ; table for each option
l1a:        INC     HL                  ; Skip 2 bytes for vector entry
            INC     HL
            DJNZ    l1a                 ; Loop until all done

            POP     BC                  ; Restore entry count
            LD      C, 0                ; Index of entry used for row position
l2:         PUSH    BC                  ; Save BC
            PUSH    HL                  ; Save HL
            LD      A, 4                ; TAB(4,B+4)
            ADD     C
            LD      B, A
            LD      C, 4
            CALL    teletextSetPos

            CALL    greenText           ; Set label colour

            POP     HL                  ; Restore HL & write label
            CALL    writeString
            POP     BC                  ; Restore BC
            INC     C                   ; next index
            DJNZ    l2                  ; Loop back

l3:         CALL    showPrompt          ; Show prompt so cursor has somewhere to flash
            CALL    getKey              ; Get key press
            CALL    menuLookup1         ; Lookup char to menu vector entry
            JR      Z, l3               ; Loop as no entry found
            JP      (HL)                ; Jump to handler

#endlocal

; Convert A to a pointer in table HL
tableLookup:
            PUSH    DE
            LD      E, A
            LD      D, 0
            ADD     HL, DE              ; Skip entry keys
            POP     DE
            RET

; Lookup key in A and return HL with value
;
; Entry:
;   A       Key to lookup
menuLookup: LD      (currentMenu), HL   ; Save HL

menuLookup1:                            ; Entry point using current menu
#local
            CALL    toUpper             ; to upper case

            LD      HL, (currentMenu)   ; Start from currentMenu
            LD      B, (HL)             ; Entry count
l0:         INC     HL                  ; Next key
            LD      C, (HL)
            CP      C                   ; Compare A to key
            JR      Z, l3               ; Key match so look up address
            DJNZ    l0                  ; Loop for next potential match

            LD      HL, 0               ; Not found so reset HL

l2:         LD      A, H                ; Test HL is not 0 to set Z
            OR      L
            RET

l3:         LD      HL, (currentMenu)   ; Find the vector entry
            LD      E, (HL)             ; Entry count
            LD      D, 0
            INC     HL                  ; Skip entry count
            ADD     HL, DE              ; Skip entry keys
            LD      A, E                ; Now we want (E-B)<<1 to get vector index
            SUB     B
            ADD     A, A                ; A<<1
            LD      E, A
            ADD     HL, DE              ; HL should point to the vector entry now
            LD      A, (HL)             ; Now get address at entry as no LD HL, (HL)
            INC     HL
            LD      H, (HL)
            LD      L, A
            JR      l2                  ; Exit setting Z if HL=0

#endlocal

currentMenu:    defw    0               ; Address of menu being displayed
