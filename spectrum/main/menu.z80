; ----------------------------------------------------------------------
; Handles a simple menu
; ----------------------------------------------------------------------

#include "../../teletextspectrum/teletext.inc"

#local
currentMenu:    defw    0               ; Address of menu being displayed

; Display a menu and wait for user to make a selection
;
; The menu definition is:
;       0 terminated title of menu
;       byte containing number of entries (n)
;       n bytes for the keys, in Upper case if letters
;       n 0 terminated strings for each option
;
; Entry:
;   HL      Address of menu definition
;
; Exit:
;   A       Key pressed
;   B       Index in menu of selected option
;   Carry   true if valid, false if break
;
showMenu::  CALL    header              ; Clear screen & show header
            LD      (currentMenu), HL   ; Save start of actual menu

            PUSH    HL
            LD      BC, 0x0402          ; Set text pos
            CALL    teletextSetPos
            POP     HL

            LD      B, (HL)             ; Show Key labels
            PUSH    BC                  ; Save row count
l1:         INC     HL                  ; inc & save HL
            LD      A, (HL)             ; Write key then down to next line
            CALL    oswrch
            CALL    backdown1           ; Back & Down 1 char
            DJNZ    l1                  ; Loop back
            INC     HL                  ; Skip last entry so HL is first label

            POP     BC                  ; Restore row count
            LD      C, 0                ; Index of entry
l2:         PUSH    BC                  ; Save B
            PUSH    HL                  ; Save HL
            LD      A, 4                ; TAB(4,B+4)
            ADD     C
            LD      B, A
            LD      C, 4
            CALL    teletextSetPos

            LD      A, 130              ; Set label colour
            CALL    oswrch

            POP     HL                  ; Restore HL & write label
            CALL    writeString
            POP     BC                  ; Restore B
            INC     C                   ; next index
            DJNZ    l2                  ; Loop back

l3:         CALL    getKey              ; Key next key
            RET     NC                  ; exit if BREAK
            CALL    toUpper             ; to upper case

            LD      HL, (currentMenu)
            LD      B, (HL)
l4:         INC     HL
            LD      C, (HL)
            CP      C
            SCF                         ; Mark carry set incase match
            RET     Z                   ; Exit as key found
            DJNZ    l4                  ; Loop for next potential match
            JP      l3                  ; Not found so loop for next key press

#endlocal
