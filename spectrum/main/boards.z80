; ----------------------------------------------------------------------
; Display station departures
; ----------------------------------------------------------------------

#include "../network/network.inc"
#include "../../teletextspectrum/teletext.inc"

#local
crsDepartures:: LD      DE, test + 7        ; Copy CRS
                LD      BC, 3
                LDIR
departures::    LD      HL, searchHeader
                CALL    header              ; Clear screen & show header

                LD      HL, test            ; Send search command with pattern
                CALL    networkSend

                LD      HL, 0x6000          ; Get result
                CALL    networkGetBlock

                LD      D, 'S'              ; Find sole Station record
                CALL    findFirst
                RET     NZ                  ; Not found so bomb out

                LD      E, (IX+5)           ; Station tiploc
                CALL    findTiplocName
                JR      Z, d0               ; Found so display full name

                PUSH    IX                  ; LD HL,IX
                POP     HL

                LD      DE, 6               ; use CRS as fallback
                ADD     HL, DE
d0:             CALL    header

                LD      HL, headers         ; Column headers
                CALL    writeString

                CALL    firstBlock          ; Now departures
d1:             CALL    findDep             ; Find next departure
                JR      NZ, k1
                CALL    displayDep          ; Display it
                CALL    nextBlock           ; move to next block
                JR      d1                  ; loop for next departure

k1:             CALL    getKey              ; Get key
                RET     NC                  ; exit on break
                RET

displayDep:     PUSH    HL
                CALL    dd0
                POP     HL
                RET
dd0:            LD      A, (IX+5)           ; Low nibble of dep
                ADD     A                   ; A = A<<1
                ADD     5

                CP      23                  ; Don't go beyond bottom of display
                RET     NC

                LD      B,A
                LD      C,0                 ; Top row
                CALL    dd1
                INC     B
dd1:            PUSH    BC
                CALL    teletextSetPos
                LD      A, 141
                CALL    oswrch

                PUSH    IX                  ; Save IX as we need a tiploc
                LD      E, (IX+18)
                CALL    findTiplocName
                JR      NZ, dd2
                CALL    writeString         ; Write it
dd2:            POP     IX                  ; Restore IX to departure record

                POP     BC                  ; Platform
                PUSH    BC
                PUSH    HL                  ; Save HL
                LD      C, 29
                CALL    teletextSetPos
                LD      A, 135
                CALL    oswrch
                PUSH    IX
                POP     HL
                LD      DE, 0x0C            ; Offset of platform
                ADD     HL, DE
                LD      B, 4
                CALL    wstrb
                POP     HL                  ; Restore HL

                POP     BC                  ; Expected time
                PUSH    BC
                LD      C, 35
                CALL    teletextSetPos
                PUSH    IX
                POP     HL
                LD      DE, 7
                ADD     HL, DE
                LD      B, 5
                ;CALL    wstrb

                POP     BC
                RET

searchHeader:   defb    "Departures", 0
headers:        defb    31,0,3,131,141,"Deprt Destination",31,30,3,"Plat Expct"
                defb    31,0,4,131,141,"Deprt Destination",31,30,4,"Plat Expct"
                ; plat 24 Deprt 29 Expected 35
                defb    0
test:           defb    "depart mde", 10, 0

#endlocal
