; ----------------------------------------------------------------------
; Interface RS232 routines
; ----------------------------------------------------------------------
if1Put:
#local
            PUSH    HL          ; Save registers for the call
            PUSH    BC
            CALL    L0D07
            POP     BC
            POP     HL
            RET

L0D07:      LD      B,$0B           ; Set bit count to eleven - 1 + 8 + 2.

            CPL                     ; Invert the bits of the character.
            LD      C,A             ; Copy the character to C.

            LD      A,$EF           ; Set to %11101111
            OUT     ($EF),A         ; Make CTS (Clear to Send) low.

            CPL                     ; reset bit 0 (other bits of no importance)
            OUT     ($F7),A         ; Make RXdata low. %00010000

            LD      HL,(if1Baud)      ; Fetch value from BAUD System Variable.
            LD      D,H             ; Copy BAUD value to DE for count.
            LD      E,L             ;

L0D1C:      DEC     DE              ; ( 6) Wait 26 * BAUD cycles
            LD      A,D             ; ( 4)
            OR      E               ; ( 4)
            JR      NZ,L0D1C        ; (12) back to BD-DEL-1

L0D21:      IN      A,($EF)         ; Read the communication port.
            AND     $08             ; isolate DTR (Data Terminal Ready) bit.
            JR      Z,L0D21         ; back, until DTR found high, to TEST-DTR

            SCF                     ; Set carry flag as the start bit.
            DI                      ; Disable Interrupts.

L0D2C:      ADC     A,$00           ; Set bit 0            76543210 <- C
            OUT     ($F7),A         ; Send RXdata the start bit.

            LD      D,H             ; Transfer the BAUD value to DE for count.
            LD      E,L             ;


L0D32:      DEC     DE              ; ( 6) Wait for 26 * BAUD
            LD      A,D             ; ( 4)
            OR      E               ; ( 4)
            JR      NZ,L0D32        ; (12) back to BD-DEL-2

            DEC     DE              ; ( 6)
            XOR     A               ; ( 4) clear rxdata bit
            SRL     C               ;      shift a bit of output byte to carry.
            DJNZ    L0D2C           ; back for 11 bits to SER-OUT-L

;   Note the last two bits will have been sent reset as C is exhausted.
            EI                      ; Enable Interrupts.

            LD      A,$01           ; Set RXdata

            LD      C,$EF           ; prepare port address.
            LD      B,$EE           ; prepare value %11101110
            OUT     ($F7),A         ; Send RXdata high.
            OUT     (C),B           ; Send CTS and comms data low - switch off RS232

L0D48:      DEC     HL              ; ( 6) The final 26 * BAUD delay
            LD      A,L             ; ( 4)
            OR      H               ; ( 4)
            JR      NZ,L0D48        ; (12) back to BD-DEL-3
            ret

#endlocal
