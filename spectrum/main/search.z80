; ----------------------------------------------------------------------
; Search for station
; ----------------------------------------------------------------------

#include "../network/network.inc"
#include "../../teletextspectrum/teletext.inc"

searchStation:
#local
                LD      HL, searchHeader
                CALL    header              ; Clear screen & show header

                LD      HL, test            ; Send search command with pattern
                CALL    networkSend

                LD      HL, 0x6000          ; Get result
                CALL    networkGetBlock

                LD      HL, searchHeader    ; Clear screen
                CALL    header

                LD      A, '0'              ; First station
l1:             PUSH    AF
                LD      D, '0'
                LD      E, A
                CALL    findStation
                JR      NZ, end             ; All done

                POP     AF                  ; Get digit
                INC     A
                PUSH    AF                  ; Save for display

                LD      C, 4
                SUB     48-4                ; ASCII to digit, Row 4 onwards
                LD      B, A
                PUSH    HL
                CALL    teletextSetPos
                POP     HL

                POP     AF                  ; Show key
                PUSH    AF
                CALL    oswrch

                LD      a, 32
                CALL    oswrch

                PUSH    HL                  ; Save HL current station

                PUSH    IX                  ; LD HL, IX
                POP     HL
                LD      DE, 7               ; Offset to CRS in station record
                ADD     HL, DE
                CALL    writeString         ; Write CRS

                LD      A, 32
                CALL    oswrch

                LD      D, (IX+5)           ; Get tiploc index, search uses
                LD      E, (IX+6)           ; same id's
                CALL    findTiplocName
                JR      NZ, l2              ; Not found, shouldn't happen
                CALL    writeString         ; Write name
l2:             POP     HL                  ; restore HL
                POP     AF                  ; Next result
                JR      l1                  ; Loop back to next result

end:            POP     AF                  ; Dump

k1:             CALL    getKey              ; Get key
                RET     NC                  ; exit on break

                DEC     A                   ; 1 becomes 0 etc
                LD      D, '0'
                LD      E, A                ; Search for station
                CALL    findStation
                JR      NZ, k1              ; Not found so next key

                PUSH    IX                  ; LD HL, IX
                POP     HL
                LD      DE, 7               ; Offset to CRS
                ADD     HL, DE
                JP      crsDepartures       ; Show station boards

searchHeader:   defb    "Search stations", 0
test:           defb    "search maid", 10, 0

wstrb::         LD      A, (HL)
                OR      A
                RET     Z
                CALL    oswrch
                INC     HL
                DJNZ    wstrb
                RET
#endlocal
